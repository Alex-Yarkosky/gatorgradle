plugins {
    id "maven"
    id "com.gradle.plugin-publish" version "0.9.10"
    id "ru.vyarus.quality" version "2.4.0"
}

ext.gitVersioner = [
        /* defaultBranch           : "master",  // default "master" */
        stableBranches          : ["master"], // the feature branch postfix (-dm4(6)) will not be appended on stable branches
        yearFactor              : 500, 	  // default "1000", increasing every 8.57h
        snapshotEnabled         : false,      // default false, the "-SNAPSHOT" postfix
        localChangesCountEnabled: false,       // default false, the (<commitCount>) before -SNAPSHOT
]
// import the script which runs the version generation
apply from: "versioner.gradle"

def id = "gatorgradle"
group = "org.gatored"
def release = "0.1"

version = "${release}.${gitVersionName}"

println("\u001B[1;36mVERSION ${version}\u001B[0m")

pluginBundle {
    website = "http://www.cs.allegheny.edu/sites/gatored"
    vcsUrl = "https://github.com/gatored/gatorgradle"

    plugins {
      gatorgradle {
          id = "${group}.${id}"
          displayName = "GatorGradle"
          description = "Gradle plugin for integrating GatorGrader as a grading task"
          tags = ["grading", "allegheny", "gatorgrader", "mdl", "proselint"]
          version = "${version}"
      }
  }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${System.properties['user.home']}/.m2/repository")
        }
    }
}

quality {
    checkstyleVersion = "8.1"

    // disable PMD for now (100+ issues to fix)
    pmd = false;

    findbugsLevel = "low"
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile gradleTestKit()
}

/* test {
    // always run tests
    outputs.upToDateWhen { false }

    // turn on console logging
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }

} */

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// add classpath file to test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

tasks.whenTaskAdded { task ->
    if (task.getName().contains("PluginMavenPublication")) {
        task.setDependsOn([])
        task.setMustRunAfter([])
        task.setShouldRunAfter([])
        task.setEnabled(false)
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
