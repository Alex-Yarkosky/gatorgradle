plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.gradle.plugin-publish" version "0.9.10"
}

ext.gitVersioner = [
        /* defaultBranch           : "master",  // default "master" */
        stableBranches          : ["master"], // the feature branch postfix (-dm4(6)) will not be appended on stable branches
        yearFactor              : 500, 	  // default "1000", increasing every 8.57h
        snapshotEnabled         : false,      // default false, the "-SNAPSHOT" postfix
        localChangesCountEnabled: false,       // default false, the (<commitCount>) before -SNAPSHOT
]
// import the script which runs the version generation
apply from: "versioner.gradle"

def version = "${artifact_version}.${gitVersionName}"

println("\u001B[1;36mVERSION ${version}\u001B[0m")

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = "${version}"
            groupId = "${artifact_group}"
            artifactId = "${artifact_name}"
            from components.java
        }
    }
}

pluginBundle {
    website = "http://www.cs.allegheny.edu/sites/gatored"
    vcsUrl = "https://github.com/gatored/gatorgradle"

    plugins {
      gatorgradle {
          id = "${group}.${artifact_name}"
          displayName = "GatorGradle"
          description = "Gradle plugin for integrating GatorGrader as a grading task"
          tags = ["grading", "allegheny", "gatorgrader", "mdl", "proselint"]
          version = "${version}"
      }
  }
}

repositories {
    mavenLocal()
    mavenCentral()
}

// perform checkstyle checking with the "gradle check" command
apply plugin: "checkstyle"
checkstyle.toolVersion = "8.1"
checkstyle {
    ignoreFailures = false
        maxWarnings = 0
        maxErrors = 0
        configFile = new File(rootDir, ".checks.xml")
}

test {
    // always run tests
    outputs.upToDateWhen { false }

    // turn on console logging
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }

}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// add classpath file to test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
